# The main entry point of your workflow.
# After configuring, running snakemake -n in a clone of this repository should successfully execute a dry-run of the workflow.

import os
import pathlib
import pandas as pd
from snakemake.io import Wildcards
from snakemake.remote.S3 import RemoteProvider as S3RemoteProvider
from snakemake.remote.HTTP import RemoteProvider as HTTPRemoteProvider
from snakemake.utils import validate
import subprocess

S3 = S3RemoteProvider()

sys.path.insert(0, ".")
from lib import target_construction as tc

shell.executable("/bin/bash")
shell.prefix("set -euo pipefail; ")

try:
    pipeline_version = subprocess.check_output(
        ["git", "describe", "--tags", "HEAD"], encoding="UTF-8"
    ).strip()
except subprocess.CalledProcessError:
    pipeline_version = "{version not detected}"
print("wgs-downstream version {}".format(pipeline_version))


configfile: "config/config.yaml"


validate(config, "../schema/global_config_schema.yaml")

tempDir = "temp"
bam_manifest = config["bam_manifest"]
gvcf_manifest = config["gvcf_manifest"]
bam_manifest = pd.read_csv(bam_manifest, sep="\t")
gvcf_manifest = pd.read_csv(gvcf_manifest, sep="\t")

validate(bam_manifest, "../schema/bam_manifest_schema.yaml")
validate(gvcf_manifest, "../schema/gvcf_manifest_schema.yaml")

bam_manifest["internal-bam-paths"] = [
    "results/{}/{}.bam".format(x, y)
    for x, y in zip(bam_manifest["projectid"], bam_manifest["sampleid"])
]

reference_build = config["genome-build"]

TARGETS = [
    "results/multiqc/multiqc.cross-flowcell.html",
    "results/glnexus/merged_callset.filtered.regions.vcf.gz",
    "results/expansionhunter_denovo/outlier_analysis/results.outlier_locus.tsv",
    "results/expansionhunter_denovo/outlier_analysis/results.outlier_motif.tsv",
]


## To deal with the fact that s3 remote provider service is very slow
## and sometimes buggy, while not interfering with a streamlined
## reference file naming convention, I'll use a snakemake functionality
## for resolving ambiguous rule determination that I really dislike otherwise.
## This is flagged for possible better resolution in the future.
ruleorder: samtools_index_fasta > download_reference_data
ruleorder: index_vcf > download_reference_data
ruleorder: adjust_fasta_formatting > download_reference_data


rule all:
    input:
        TARGETS,


rule somalier:
    input:
        [x for x in filter(lambda y: "multiqc" in y, TARGETS)],


rule expansionhunter_denovo:
    input:
        [x for x in filter(lambda y: "expansionhunter" in y, TARGETS)],


rule glnexus:
    input:
        "results/glnexus/merged_callset.filtered.regions.vcf.gz",


include: "rules/acquire_data.smk"
include: "rules/expansionhunter_denovo.smk"
include: "rules/glnexus.smk"
include: "rules/multiqc.smk"
include: "rules/references.smk"
include: "rules/somalier.smk"
